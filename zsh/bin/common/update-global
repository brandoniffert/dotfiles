#!/usr/bin/env zsh

p_header() {
  local title="$*"
  local length="${#title}"
  local cols=$(($(tput cols) - length - 3))
  printf "\n[$(tput setaf 3)%s$(tput sgr0)] " "$title"
  repeat $cols printf "Â·"
  printf "\n"
}

if (( $+commands[npm] )); then
  p_header 'npm'
  npm outdated --location=global
  ret=$?
  if [[ "$ret" -eq 1 ]]; then
    printf "\n"
    read "response?> Do you want to update? [y|N] "
    if [[ $response =~ ^[yY](es)?$ ]]; then
      printf "\n"
      npm upgrade --location=global
    fi
    unset response
  else
    echo '* Up to date, skipping...'
  fi
fi

if (( $+commands[composer] )); then
  p_header 'composer'
  composer global outdated --direct --strict
  ret=$?
  if [[ "$ret" -eq 1 ]]; then
    printf "\n"
    packages=($(composer global show --direct 2>/dev/null | grep '^[a-zA-Z]' | cut -d ' ' -f1))
    composer global update --interactive --with-dependencies "${packages[@]}"
  fi
fi

if (( $+commands[tmux] )); then
  p_header 'tmux'
  if [[ -n "$TMUX" ]]; then
    "${XDG_DATA_HOME}/tmux/plugins/tpm/scripts/update_plugin.sh" "" all
    "${XDG_DATA_HOME}/tmux/plugins/tpm/scripts/source_plugins.sh"
  else
    echo '! Not inside a tmux session, skipping...'
  fi
fi

if [[ -f ${ZDOTDIR:-~}/.antidote/antidote.zsh ]]; then
  p_header 'antidote'
  source ${ZDOTDIR:-~}/.antidote/antidote.zsh
  antidote update
fi

if (( $+commands[determinate-nixd] )); then
  p_header 'determinate-nixd'
  sudo determinate-nixd upgrade
fi

printf "\n"
unset -f p_header
