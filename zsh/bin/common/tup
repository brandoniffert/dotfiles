#!/usr/bin/env zsh

set -e

# Setup two windows in a tmux session or custom layout if .tmux file exists

if [[ -n "$TMUX" ]] || [[ -n "$TMUX_PANE" ]]; then
  if [[ $(tmux list-windows | grep '' -c) -eq 1 ]]; then
    # Check for .tmux file (poor man's Tmuxinator)
    if [[ -x .tmux ]]; then
      # Prompt the first time we see a given .tmux file before running it
      digest="$(openssl sha512 .tmux)"
      if ! grep -q "$digest" "$XDG_DATA_HOME/.tmux-digests" 2> /dev/null; then
        cat .tmux
        echo
        read -k 1 -r \
          'REPLY?Trust (and run) this .tmux file? (t = trust, otherwise = skip) '
        echo
        if [[ $REPLY =~ ^[Tt]$ ]]; then
          echo "$digest" >> "$XDG_DATA_HOME/.tmux-digests"
          ./.tmux
          return
        fi
      else
        ./.tmux
        return
      fi
    else
      tmux rename-window 'code'
      tmux new-window
      tmux rename-window 'build'
      tmux select-window -t 1
      clear
    fi
  fi
fi
