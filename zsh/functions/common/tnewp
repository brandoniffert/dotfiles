# shellcheck shell=bash

# Create a new tmux session within a project folder and attach to it
# If the session already exists, attach to that one

emulate -L zsh

# Fail if PROJECTS_HOME is not set
if [[ -z ${PROJECTS_HOME+x} ]]; then
  echo "Please set \$PROJECTS_HOME"
  sleep 2
  exit 1
fi

# Modify display if inside a tmux popup
if [[ -n "$TMUX_POPUP" ]]; then
  fzf_args=(--border none)
else
  fzf_args=(--height 40% --border sharp)
fi

find_output=$(find "$PROJECTS_HOME" -mindepth 2 -maxdepth 2 -type d | sed "s:$PROJECTS_HOME\/::g")
project_dir=$(echo "$find_output" | fzf --header='Projects' "${fzf_args[@]}")

if [[ -n "${project_dir}" ]]; then
  project_path="$PROJECTS_HOME/$project_dir"
  session_name=$(basename "$project_path" | sed 's/\./-/g')

  if [[ -z "$session_name" ]]; then
    exit 1
  fi

  # If session doesn't exist, create it
  if ! tmux -q has-session -t "$session_name" >/dev/null 2>&1; then
    # Convention for docker based projects
    if [[ -d "$project_path/site" ]]; then
      tmux new-session -c "$project_path/site" -d -s "$session_name"
    else
      tmux new-session -c "$project_path" -d -s "$session_name"
    fi
  fi

  clear

  # Attach or switch to it, depending on whether we are already in a tmux session
  if [[ -n "$TMUX" ]] || [[ -n "$TMUX_PANE" ]]; then
    tmux switch-client -t "$session_name"
  else
    tmux attach-session -t "$session_name"
  fi
fi
